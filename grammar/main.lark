%import common.WS
%import common.CNAME   -> NAME
%import common.INT
%import common.ESCAPED_STRING
%ignore WS

COMMENT: "#" /[^\n]/*
%ignore COMMENT

assign_stmt: "set" NAME "to" expression

when_stmt: "when" expression block ["otherwise" block]

op: "=" -> equal
    | ">" -> more
    | "<" -> less
    | "!=" -> notequal
    | "+" -> plus
    | "*" -> multiply

print_stmt: "print" expression

tool_stmt: "tool" NAME params block

params: "(" [param ("," param)*] ")"
param: NAME

call_stmt: "call" NAME "(" [expression ("," expression)*] ")"

?expression: primary
           | primary op primary -> comparison_expr

?primary: NAME      -> name
        | INT       -> int
        | ESCAPED_STRING -> string
        | "true"    -> true
        | "false"   -> false

block: "{" stmt* "}"

?stmt: assign_stmt
     | when_stmt
     | print_stmt
     | tool_stmt
     | call_stmt

start: stmt*
